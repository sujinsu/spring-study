## AWS & Terraform [Terraform]

Terraform은 IaC(Infrastructure as Code) 도구로서 그래픽 사용자 인터페이스가 아닌 구성 파일을 사용하여 인프라를 관리할 수 있습니다. 이를 통해 사람이 읽을 수 있는 선언적 구성 파일에서 리소스와 인프라를 정의하고 인프라의 수명 주기를 관리할 수 있습니다. 

Terraform을 사용하면 인프라를 수동으로 관리하는 것보다 몇 가지 이점이 있습니다.

* Terraform은 여러 클라우드 플랫폼에서 인프라를 관리할 수 있습니다.
* 사람이 읽을 수 있는 구성 언어를 사용하면 인프라 코드를 빠르게 작성할 수 있습니다.
* Terraform의 상태를 통해 배포 전반에 걸쳐 리소스 변경 사항을 추적할 수 있습니다.
* 인프라에서 안전하게 협업하기 위해 구성을 버전 제어에 커밋할 수 있습니다.


Terraform으로 배포하기 위한 단계입니다.

1. Scope - 프로젝트의 인프라를 식별합니다.
2. Author - 인프라에 대한 구성을 작성합니다.
3. Initialize - Terraform이 인프라를 관리하는 데 필요한 플러그인을 설치합니다.
4. Plan - 구성과 일치하도록 Terraform이 수행할 변경 사항을 미리 봅니다.
5. Apply - 계획된 변경을 수행합니다.




### Terraform 설치

시스템이 최신 상태이고 gnupg, software-properties-common 및 curl 패키지가 설치되어 있는지 확인합니다.
이 패키지를 사용하여 HashiCorp의 GPG 서명을 확인하고 HashiCorp의 Debian 패키지 저장소를 설치합니다.
``` shell
$ sudo apt-get update && sudo apt-get install -y gnupg software-properties-common curl
```

HashiCorp GPG 키를 추가합니다.
``` shell
$ curl -fsSL https://apt.releases.hashicorp.com/gpg | sudo apt-key add -
```

공식 HashiCorp Linux 저장소를 추가합니다.
``` shell
$ sudo apt-add-repository "deb [arch=amd64] https://apt.releases.hashicorp.com $(lsb_release -cs) main"
```

업데이트하여 저장소를 추가하고 Terraform CLI를 설치합니다.
``` shell
$ sudo apt-get update && sudo apt-get install terraform
```




#### 설치 확인

Download 또는 Install이 정상적으로 되었는지는 아래의 방법으로 쉽게 확인이 가능하다.
```
$ terraform version
Terraform v1.1.9
on linux_amd64
```



### 인프라 구축

#### 전제조건 (Credentials 설정)

IAM 자격 증명을 사용하여 Terraform AWS 공급자를 인증하려면 AWS_ACCESS_KEY_ID 환경 변수를 설정합니다.
``` shell
$ export AWS_ACCESS_KEY_ID="<YOUR_AWS_ACCESS_KEY_ID>"
```

비밀 키를 설정합니다.
``` shell
$ export AWS_SECRET_ACCESS_KEY="<YOUR_AWS_SECRET_ACCESS_KEY>"
```

AWS_DEFAULT_REGION 환경 변수를 설정합니다.
``` shell
$ export AWS_DEFAULT_REGION="<YOUR_AWS_DEFAULT_REGION>"
```




#### Write 구성

각 Terraform 구성은 자체 작업 디렉토리에 있어야 합니다.
구성을 위해 디렉터리 생성합니다.
``` shell
$ mkdir learn-terraform-aws-instance
```

디렉터리를 이동합니다.
``` shell
$ cd learn-terraform-aws-instance
```

인프라를 정의하는 파일을 작성합니다. 

중복되는 값은 변수로 선언해 따로 관리하기 위해 main.tf와 variable.tf로 분리하여 작성합니다. variable.tf 파일에 정의한 변수는 main.tf 파일에서 var.변수명 으로 사용할 수 있습니다.
##### main.tf
``` shell
$ vi main.tf
```

```

terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 3.27"
    }
  }

  required_version = ">= 0.14.9"
}

provider "aws" {
  region = var.region
}

resource "aws_vpc" "vpc" {
  cidr_block = "10.0.0.0/16"

  tags = {
    Name = "VPC_Platform"
  }
}

resource "aws_subnet" "subnet" {
  vpc_id            = aws_vpc.vpc.id
  cidr_block        = "10.0.0.0/24"
  availability_zone = var.region

  tags = {
    Name = "Platform-Common-DMZ-A"
  }
}

resource "aws_network_interface" "network" {
  subnet_id   = aws_subnet.subnet.id
  #private_ips = [""]

  tags = {
    Name = "${var.resource_name}_interface"
  }
}


resource "aws_instance" "terraform_test_server" {
  ami           = var.image_id
  instance_type = "t3.nano"

  ebs_block_device {
    device_name = var.device_name
    volume_size = var.volume_size
    volume_type = var.volume_type
  }

  network_interface {
    network_interface_id = aws_network_interface.network.id
    device_index         = 0
  }

  tags = {
    Name = "${var.resource_name}_server"
  }
}

```
##### variable.tf

``` shell
$ vi variable.tf
```

```
variable "region" {
  description = "AWS 지역 (서울)"
  default     = "ap-northeast-2"
}
variable "http_port" {
  description = "80 port"
  default     = 80
}
variable "resource_name" {
  description = "자원  이름"
  type        = string
  default     = "terraform_test"
}
variable "image_id" {
  description = "내EC2 instance AMI"
  type        = string
  default     = "ami-830c94e3"
}
variable "volume_size" {
  description = "volume 사이즈(GB)"
  default     = 15
}
variable "volume_type" {
  description = "volume 타입"
  default     = "gp2"
}
variable "device_name" {
  description = "volume 마운트 할 장치 이름"
  default     = "/dev/sda1"
}
```

#### Terraform
  
 Terraform 블록은 인프라를 프로비저닝하는 데 사용할 필수 공급자를 포함한 Terraform 설정이 포함됩니다. 각 공급자에 대해 source특성은 **선택적 호스트 이름, 네임스페이스 및 공급자 유형을 정의**합니다.

 required_providers 블록에 정의된 각 공급자에 대한 **버전 제약 조건을 설정**할 수도 있습니다. version 속성은 선택이지만 Terraform이 구성과 작동하지 않는 공급자 버전을 설치하지 않도록 공급자 버전을 제한하는 데 사용하는 것이 좋습니다. 제공자 버전을 지정하지 않으면 Terraform은 초기화 중에 가장 최신 버전을 자동으로 다운로드합니다.

 버전을 선택하는 방법은 아래와 같습니다.

> =: 특정 버전을 명시할 때 사용하며, 다른 condition 과 함께 사용할 수 없다. operator 를 쓰지 않아도 동일하므로 보통은 쓰지 않는다.
> 
>   version = "= 3.14.0"
>   version = "3.14.0"
>
>
> `!=`: 특정 버전을 제외한다. 테스트 해본 결과 특정 버전을 제외 후 이미 install 된 provider 가 있다면 해당 provider 버전을 사용하고, 없다면 특정 버전을 제외한 최신 provider를 install 하여 사용한다.
>   version = "!= 3.14.0"
>   version = "!= 3.14.0, != 3.14.1, != 3.15.0"
>
>
> `>`, `>=`, `<`, `<=`: 특정 버전을 기준으로 높거나 낮은 버전을 선택할 때 사용한다.
>   version = "> 3.14.0"  # 3.14.0 초과 버전
>   version = ">= 3.14.0" # 3.14.0 이상 버전
>   version = "< 3.14.0"  # 3.14.0 미만 버전
>   version = "<= 3.14.0" # 3.14.0 이하 버전
>   version = "> 3.14.0, <= 3.15.0" # 3.14.0 초과, 3.15.0 이하 버전
>
>
> `~>`: 특정 버전과 해당 버전의 가장 낮은 버전 대역의 최신 버전까지 지원하고자 할 때 사용한다. 
>   version = "~> 3.14.1" # 3.14.1 이상, 3.15.0 미만 버전 (>= 3.14.1, < 3.15)
>   version = "~> 3.14" # 3.14 이상, 4.0 미만 버전 (>= 3.14, < 4.0)


#### Provider

Provider 블록은 지정된 공급자(AWS, GCP, AZURE...)에 대한 설정을 수행합니다. Provider는 Terraform이 code를 수행하기 위해 필요한 정보들을 제공해주는 역할을 수행합니다.

예시에서 사용한 arguments :

* region : Resource들을 배포하고자 하는 지역을 설정합니다.

* AWS credential 파일이 존재한다면 아래와 같이 Credentials 설정을 진행할 수 있습니다.

``` shell
provider "aws" {
  shared_credentials_file = "/Users/tf_user/.aws/creds" # Credential 파일 위치
  profile                 = "customprofile" # Credential 파일 내의 저장된 profile 중 선택
}
```

* [default tag](https://registry.terraform.io/providers/hashicorp/aws/latest/docs#default_tags-configuration-block) 를 설정할 수 있습니다. 

``` shell
provider "aws" {
  default_tags {
    tags = {
      Environment     = "Test"
      Service         = "Example"
    }
  }
}
```

* [aws provider에서 사용할 수 있는 arguments](https://registry.terraform.io/providers/hashicorp/aws/latest/docs#argument-reference)

#### Resource

Resource 블록은 인프라의 구성 요소를 정의합니다.

리소스 블록에는 블록 앞에 리소스 타입과 리소스 네임이라는 두 개의 문자열이 있습니다. 이 예에서 리소스 타입은 aws_instance이고 네임은 app_server입니다. 리소스 유형과 리소스 이름은 함께 리소스의 고유 ID를 형성합니다. 예를 들어 EC2 인스턴스의 ID는 입니다 aws_instance.app_server.

리소스 블록에는 리소스를 구성하는 데 사용하는 argument가 포함되어 있습니다. 리소스 타입에 따라 사용하는 argument들이 다릅니다..

aws_instance 예시에서 사용한 arguments :

* ami : (선택 사항) 인스턴스에 사용할 AMI. launch_template가 지정되고 시작 템플릿이 AMI를 지정 하지 않는 한 필수 입니다.
  
* instance_type : (선택 사항) 인스턴스에 사용할 인스턴스 유형입니다. 이 필드를 업데이트하면 EC2 인스턴스의 중지/시작이 트리거됩니다.
  
* ebs_block_device : EBS(Elastic Block Store) 로 생성한 디스크
  * device_name : (필수) 마운트할 장치의 이름입니다.
  * volume_size : (선택 사항) 기비바이트(GiB) 단위의 볼륨 크기입니다.
  * volume_type : (선택 사항) 볼륨 유형입니다. 유효한 값은 standard, gp2, gp3, io1, io2, sc1또는 st1입니다. 기본값은 gp2입니다.
  
* network_interface : 부팅 시 네트워크 인터페이스를 EC2 인스턴스에 연결합니다.
  * network_interface_id : (필수) 연결할 네트워크 인터페이스의 ID입니다.
  * device_index : 첨부 순서에서 네트워크 인터페이스의 위치입니다. 기본 네트워크 인터페이스의 장치 인덱스는 0입니다. 인스턴스를 시작할 때 네트워크 인터페이스를 생성하는 경우 디바이스 인덱스를 지정해야 합니다.
  
* tags : (선택 사항) 리소스에 할당할 태그 맵입니다. <https://registry.terraform.io/providers/hashicorp/aws/latest/docs#default_tags-configuration-block>

* [ws_instance 타입에서 사용할 수 있는 arguments](https://registry.terraform.io/providers/hashicorp/aws/latest/docs/resources/instance#argument-reference)




#### 디렉터리 초기화

새 구성을 생성하거나 버전 제어에서 기존 구성을 체크아웃할 때 디렉토리를 **terraform init**로 초기화해야 합니다 

구성 디렉토리를 초기화하면 구성에 정의된 공급자(이 경우 aws공급자)가 다운로드 및 설치됩니다.

``` shell
$ terraform init

Initializing the backend...

Initializing provider plugins...
- Reusing previous version of hashicorp/aws from the dependency lock file
- Using previously-installed hashicorp/aws v3.75.1

Terraform has been successfully initialized!

You may now begin working with Terraform. Try running "terraform plan" to see
any changes that are required for your infrastructure. All Terraform commands
should now work.

If you ever set or change modules or backend configuration for Terraform,
rerun this command to reinitialize your working directory. If you forget, other
commands will detect it and remind you to do so if necessary.

```




#### 구성 형식 지정 및 유효성 검사

terraform fmt명령은 가독성과 일관성을 위해 현재 디렉토리의 구성을 자동으로 업데이트합니다.
``` shell
$ terraform fmt
```

terraform validate 명령을 사용하여 구성이 구문적으로 유효하고 내부적으로 일관성이 있는지 확인할 수 있습니다.
위에 제공된 예시 구성은 유효하므로 Terraform은 성공 메시지를 반환합니다.
``` shell
$ terraform validate
Success! The configuration is valid.
```




#### Terrafrom Plan

``` shell
$ terraform plan

Terraform used the selected providers to generate the following execution plan. Resource actions are indicated with the following symbols:
  + create

Terraform will perform the following actions:

  # aws_instance.app_server will be created
  + resource "aws_instance" "app_server" {
      + ami                                  = "ami-830c94e3"
      + arn                                  = (known after apply)
      + associate_public_ip_address          = (known after apply)
      + availability_zone                    = (known after apply)
      + cpu_core_count                       = (known after apply)
      + cpu_threads_per_core                 = (known after apply)
      + disable_api_termination              = (known after apply)
      + ebs_optimized                        = (known after apply)
      + get_password_data                    = false
      + host_id                              = (known after apply)
      + id                                   = (known after apply)
      + instance_initiated_shutdown_behavior = (known after apply)
      + instance_state                       = (known after apply)
      + instance_type                        = "t2.micro"
      + ipv6_address_count                   = (known after apply)
      + ipv6_addresses                       = (known after apply)
      + key_name                             = (known after apply)
      + monitoring                           = (known after apply)
      + outpost_arn                          = (known after apply)
      + password_data                        = (known after apply)
      + placement_group                      = (known after apply)
      + placement_partition_number           = (known after apply)
      + primary_network_interface_id         = (known after apply)
      + private_dns                          = (known after apply)
      + private_ip                           = (known after apply)
      + public_dns                           = (known after apply)
      + public_ip                            = (known after apply)
      + secondary_private_ips                = (known after apply)
      + security_groups                      = (known after apply)
      + source_dest_check                    = true
      + subnet_id                            = (known after apply)
      + tags                                 = {
          + "Name" = "ExampleAppServerInstance"
        }
      + tags_all                             = {
          + "Name" = "ExampleAppServerInstance"
        }
      + tenancy                              = (known after apply)
      + user_data                            = (known after apply)
      + user_data_base64                     = (known after apply)
      + vpc_security_group_ids               = (known after apply)

      + capacity_reservation_specification {
          + capacity_reservation_preference = (known after apply)

          + capacity_reservation_target {
              + capacity_reservation_id = (known after apply)
            }
        }
        vpc subnet mask

      + ebs_block_device {
          + delete_on_termination = (known after apply)
          + device_name           = (known after apply)
          + encrypted             = (known after apply)
          + iops                  = (known after apply)
          + kms_key_id            = (known after apply)
          + snapshot_id           = (known after apply)
          + tags                  = (known after apply)
          + throughput            = (known after apply)
          + volume_id             = (known after apply)
          + volume_size           = (known after apply)
          + volume_type           = (known after apply)
        }

      + enclave_options {
          + enabled = (known after apply)
        }

      + ephemeral_block_device {
          + device_name  = (known after apply)
          + no_device    = (known after apply)
          + virtual_name = (known after apply)
        }

      + metadata_options {
          + http_endpoint               = (known after apply)
          + http_put_response_hop_limit = (known after apply)
          + http_tokens                 = (known after apply)
          + instance_metadata_tags      = (known after apply)
        }

      + network_interface {
          + delete_on_termination = (known after apply)
          + device_index          = (known after apply)
          + network_interface_id  = (known after apply)
        }

      + root_block_device {
          + delete_on_termination = (known after apply)
          + device_name           = (known after apply)
          + encrypted             = (known after apply)
          + iops                  = (known after apply)
          + kms_key_id            = (known after apply)
          + tags                  = (known after apply)
          + throughput            = (known after apply)
          + volume_id             = (known after apply)
          + volume_size           = (known after apply)
          + volume_type           = (known after apply)
        }
    }

Plan: 1 to add, 0 to change, 0 to destroy.

─────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────────

Note: You didn't use the -out option to save this plan, so Terraform can't guarantee to take exactly these actions if you run "terraform apply" now.
```





#### 인프라 생성

terraform apply 명령을 사용하여 구성을 적용한다.
``` shell
$ terraform apply

An execution plan has been generated and is shown below.
Resource actions are indicated with the following symbols:
  + create

Terraform will perform the following actions:

  # aws_instance.app_server will be created
  + resource "aws_instance" "app_server" {
      + ami                          = "ami-830c94e3"
      + arn                          = (known after apply)
##...

Plan: 1 to add, 0 to change, 0 to destroy.

Do you want to perform these actions?
  Terraform will perform the actions described above.
  Only 'yes' will be accepted to approve.

  Enter a value:
```




#### 상태 검사

Terraform은 terraform.tfstate에 관리하는 리소스의 ID와 속성을 저장하므로 앞으로 해당 리소스를 업데이트하거나 삭제할 수 있습니다.

Terraform 상태 파일은 Terraform이 관리하는 리소스를 추적할 수 있습니다. 민감한 정보를 포함하는 경우가 많으므로 상태 파일을 안전하게 저장하고 인프라를 관리해야 하는 신뢰할 수 있는 팀원만 액세스하도록 제한해야 합니다.

**terraform show**로 현재 상태를 검사할 수 있습니다.

``` shell
$ terraform show
```